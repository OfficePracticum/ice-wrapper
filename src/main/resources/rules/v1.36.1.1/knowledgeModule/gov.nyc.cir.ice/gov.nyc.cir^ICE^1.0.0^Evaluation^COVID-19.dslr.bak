/**
 * Copyright (C) 2022 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */

package knowledgeModule.gov.nyc.cir.ice

import java.util.List
import java.util.Set
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseRule
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../../knowledgeCommon/org.cdsframework.ice/org.cdsframework^ICE^1.0.0.dsl

global java.util.Date evalTime


//////////////////////////////////////////////////////////////////////////////////////////////////
// Evaluation of vaccines not authorized by the FDA or WHO
// If the patient has received a COVID-19 vaccine not authorized by the FDA or WHO, then evaluate the shot as Invalid/VACCINE_NOT_APPROVED_IN_US_OR_BY_WHO
//////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19: If the vaccine administered is authorized neither by the FDA nor WHO, evaluate the shot as INVALID/VACCINE_NOT_APPROVED_IN_US_OR_BY_WHO"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the vaccine administered a member of ("ICE500", "ICE501", "ICE503", "ICE504", "ICE505", "ICE506", "ICE507", "ICE508", "ICE509", "ICE513", "ICE514", "ICE515", "ICE516", "ICE517")
			- make note of the associated series as $targetSeries
	then
		Include the reason for shot $currentShot Invalid due to "Vaccine Not Approved for Use in the U.S. or by the WHO"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


//////////////////////////////////////////////////////////////////////////////////////////////////
// CVX 218, if evaluated as Invalid due to being above the maximum age for that vaccine, should be ignored for purposes of recommendations
//////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19: If CVX 218 was evaluated and Not Valid due to ABOVE_MAXIMUM_AGE_VACCINE, ignore the shot when calculating intervals"
	dialect "mvel"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	when
		There is an administered shot $administeredShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- that has already been evaluated and whose Shot Validity Status is not VALID
			- make note of all evaluation reasons for this shot as $collectionOfStrReasons
			- the collection $collectionOfStrReasons contains "EVALUATION_REASON_CONCEPT.ABOVE_MAXIMUM_AGE_VACCINE"
			- make note of the Associated Series as $associatedTargetSeries
	then
		Mark the shot $administeredShot as Ignored
		Record that this dose rule was Processed for the TargetDose $administeredShot
		Log that this dose rule fired for the dose $administeredShot in the Series $associatedTargetSeries
end


//////////////////////////////////////////////////////////////////////////////////////////////////
// START Rules for an Additional Dose and Booster Dose
//////////////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////
// START Pfizer, Moderna or Authorized WHO Series Additional/Booster Logic
//////////////////////////////////////////////////////////////////////

rule "COVID-19: If the series is complete, override allowable vaccine check, as the allowable vaccines for additional and booster doses are verified by each rule"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the series that the shot belongs to is complete
			- make note of the associated series as $associatedTargetSeries
	then
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end
	

rule "COVID-19: If the patient is >= 5 yrs and < 18 yrs of age and an Additional Dose is administered in the __Moderna Series__, the shot is permitted"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the shot belongs to the Series with name "COVID19ModernaSeries"
			- the vaccine administered a member of ("ICE207", "ICE208", "ICE212", "ICE213", "ICE217", "ICE218", "ICE219", "ICE221", "ICE227", "ICE228")
			- make note of the date this shot was administered as $currentShotDate
			- make note of the associated series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the series is complete
			- make note of the dose number after which the Series was marked complete as $doseNumberAfterWhichComplete
			- the effective number of doses administered in the Series is < $doseNumberAfterWhichComplete+1
		The patient information $oEvaluatedPersonAbstract must be known to complete writing this rule
			- make note of the date as $dateAt5yOfAge when the patient is "5y" of age
			- make note of the date as $dateAt18yOfAge when the patient is "18y" of age
			- the date $currentShotDate >= $dateAt5yOfAge
			- the date $currentShotDate < $dateAt18yOfAge
	then
		Set the Shot Status of $currentShot to Valid
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


rule "COVID-19: If the patient is >= 18 yrs and < 50 yrs of age and an Additional Dose and/or 1st Booster Dose is administered in the __Moderna Series__, the shot is permitted"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the shot belongs to the Series with name "COVID19ModernaSeries"
			- the vaccine administered a member of ("ICE207", "ICE208", "ICE212", "ICE213", "ICE217", "ICE218", "ICE219", "ICE221", "ICE227", "ICE228")
			- make note of the date this shot was administered as $currentShotDate
			- make note of the associated series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the series is complete
			- make note of the dose number after which the Series was marked complete as $doseNumberAfterWhichComplete
			- the effective number of doses administered in the Series is < $doseNumberAfterWhichComplete+2
		The patient information $oEvaluatedPersonAbstract must be known to complete writing this rule
			- make note of the date as $dateAt18yOfAge when the patient is "18y" of age
			- make note of the date as $dateAt50yOfAge when the patient is "50y" of age
			- the date $currentShotDate >= $dateAt18yOfAge
			- the date $currentShotDate < $dateAt50yOfAge
	then
		Set the Shot Status of $currentShot to Valid
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end


rule "COVID-19: If the patient is >= 5 yrs and < 50 yrs of age and an Additional Dose and/or 1st Booster Dose is administered in the __Pfizer, Mixed Product, Janssen or Series not authorized by the FDA but authorized by the WHO__, the shot is permitted"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the shot belongs to the Series with name a member of ("COVID19PfizerSeries", "COVID19MixedProductSeries", "COVID19Janssen1DoseSeries", "COVID19AstraZeneca2DoseSeries", "COVID19BIBPSinopharm2DoseSeries", "COVID19CoronaVacSinovac2DoseSeries", "COVID19COVAXIN2DoseSeries", "COVID19Novavax2DoseSeries", "COVID19Medicago2DoseSeries")
			- the vaccine administered a member of ("ICE207", "ICE208", "ICE212", "ICE213", "ICE217", "ICE218", "ICE219", "ICE221", "ICE227", "ICE228")
			- make note of the date this shot was administered as $currentShotDate
			- make note of the associated series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the series is complete
			- make note of the dose number after which the Series was marked complete as $doseNumberAfterWhichComplete
			- the effective number of doses administered in the Series is < $doseNumberAfterWhichComplete+2
		The patient information $oEvaluatedPersonAbstract must be known to complete writing this rule
			- make note of the date as $dateAt5yOfAge when the patient is "5y" of age
			- make note of the date as $dateAt50yOfAge when the patient is "50y" of age
			- the date $currentShotDate >= $dateAt5yOfAge
			- the date $currentShotDate < $dateAt50yOfAge
	then
		Set the Shot Status of $currentShot to Valid
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


rule "COVID-19: If the patient is >= 50 yrs of age and an Additional Dose, 1st Booster Dose and 2nd Booster Dose is administered in the __Pfizer, Moderna, Mixed Product, Janssen or Series not authorized by the FDA but authorized by the WHO__, the shot is permitted"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the vaccine administered a member of ("ICE207", "ICE208", "ICE212", "ICE213", "ICE217", "ICE218", "ICE219", "ICE221", "ICE227", "ICE228")
			- make note of the date this shot was administered as $currentShotDate
			- make note of the associated series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the series is complete
			- make note of the dose number after which the Series was marked complete as $doseNumberAfterWhichComplete
			- the effective number of doses administered in the Series is < $doseNumberAfterWhichComplete+3
		The patient information $oEvaluatedPersonAbstract must be known to complete writing this rule
			- make note of the date as $dateAt50yOfAge when the patient is "50y" of age
			- the date $currentShotDate >= $dateAt50yOfAge
	then
		Set the Shot Status of $currentShot to Valid
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


rule "COVID-19: If an extra dose is administered to an already completed __Pfizer, Mixed Product, Moderna or WHO  Series__ prior to 28 days, include supplemental text that follow guidelines regarding immunocompromised minimum ages/intervals"
	dialect "mvel"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the series is complete
			- the name of the Series a member of ("COVID19PfizerSeries", "COVID19ModernaSeries", "COVID19MixedProductSeries", "COVID19AstraZeneca2DoseSeries", "COVID19BIBPSinopharm2DoseSeries", "COVID19CoronaVacSinovac2DoseSeries", "COVID19COVAXIN2DoseSeries", "COVID19Novavax2DoseSeries", "COVID19Medicago2DoseSeries")
			- make note of the dose number after which the Series was marked complete as $numberOfDosesRequired
			- the effective number of doses administered in the Series is == $numberOfDosesRequired+1
		There is an administered shot $administeredShot
			- the shot belongs to the series $targetSeries
			- that has already been evaluated and whose Shot Validity Status is VALID
			- the dose number in the Series is == $numberOfDosesRequired+1
			- make note of the date this shot was administered as $currentShotDate
			- make note of the associated series as $associatedTargetSeries
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- that has already been evaluated
			- the administration date of the shot is > $currentShotDate
		There is an administered shot $priorShot
			- the shot belongs to the series $targetSeries
			- the administration date of the shot is < $currentShotDate
			- make note of the date this shot was administered as $priorShotDate
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- the administration date of the shot is > $priorShotDate
			- the administration date of the shot is < $currentShotDate
		Confirm elapsed time between $priorShotDate and $currentShotDate < "28d"
	then
		Include Supplemental Text "The timing of the administration of this shot does not follow the guidelines regarding the minimum interval of 28 days required for an Additional Dose for immunocompromised patients or the minimum interval of 5 months required for the 1st Booster Dose." for Valid Shot $administeredShot
		Record that this dose rule was Processed for the TargetDose $administeredShot
        Log that this dose rule fired for the dose $administeredShot in the Series $targetSeries
end


rule "COVID-19: If an extra dose is administered to an already completed __Pfizer, Mixed Product, Moderna or WHO  Series__ >= 28 days and < 5 months, include supplemental text that follow guidelines regarding minimum ages/intervals"
	dialect "mvel"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the series is complete
			- the name of the Series a member of ("COVID19PfizerSeries", "COVID19ModernaSeries", "COVID19MixedProductSeries", "COVID19Janssen1DoseSeries", "COVID19AstraZeneca2DoseSeries", "COVID19BIBPSinopharm2DoseSeries", "COVID19CoronaVacSinovac2DoseSeries", "COVID19COVAXIN2DoseSeries", "COVID19Novavax2DoseSeries", "COVID19Medicago2DoseSeries")
			- make note of the dose number after which the Series was marked complete as $numberOfDosesRequired
			- the effective number of doses administered in the Series is == $numberOfDosesRequired+1
		There is an administered shot $administeredShot
			- the shot belongs to the series $targetSeries
			- that has already been evaluated and whose Shot Validity Status is VALID
			- the dose number in the Series is == $numberOfDosesRequired+1
			- make note of the date this shot was administered as $currentShotDate
			- make note of the associated series as $associatedTargetSeries
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- that has already been evaluated
			- the administration date of the shot is > $currentShotDate
		There is an administered shot $priorShot
			- the shot belongs to the series $targetSeries
			- the administration date of the shot is < $currentShotDate
			- make note of the date this shot was administered as $priorShotDate
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- the administration date of the shot is > $priorShotDate
			- the administration date of the shot is < $currentShotDate
		Confirm elapsed time between $priorShotDate and $currentShotDate >= "28d"
		Confirm elapsed time between $priorShotDate and $currentShotDate < "5m"
	then
		Include Supplemental Text "The timing of the administration of this shot does not follow the guidelines regarding the minimum interval of 5 months required for the 1st Booster Dose." for Valid Shot $administeredShot
		Record that this dose rule was Processed for the TargetDose $administeredShot
        Log that this dose rule fired for the dose $administeredShot in the Series $targetSeries
end

//////////////////////////////////////////////////////////////////////
// END Pfizer, Moderna or Authorized WHO Series Additional/Booster Logic
//////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////
// START Janssen Series Additional/Booster Logic
//////////////////////////////////////////////////////////////////////

rule "COVID-19: If an extra dose is administered to an already completed __Janssen__ prior to 28 days, include supplemental text that follow guidelines regarding immunocompromised minimum ages/intervals"
	dialect "mvel"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the series is complete
			- the name of the Series is "COVID19Janssen1DoseSeries"
			- make note of the dose number after which the Series was marked complete as $numberOfDosesRequired
			- the effective number of doses administered in the Series is == $numberOfDosesRequired+1
		There is an administered shot $administeredShot
			- the shot belongs to the series $targetSeries
			- that has already been evaluated and whose Shot Validity Status is VALID
			- the dose number in the Series is == $numberOfDosesRequired+1
			- make note of the date this shot was administered as $currentShotDate
			- make note of the associated series as $associatedTargetSeries
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- that has already been evaluated
			- the administration date of the shot is > $currentShotDate
		There is an administered shot $priorShot
			- the shot belongs to the series $targetSeries
			- the administration date of the shot is < $currentShotDate
			- make note of the date this shot was administered as $priorShotDate
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- the administration date of the shot is > $priorShotDate
			- the administration date of the shot is < $currentShotDate
		Confirm elapsed time between $priorShotDate and $currentShotDate < "28d"
	then
		Include Supplemental Text "The timing of the administration of this shot does not follow the guidelines regarding the minimum interval of 28 days required for an Additional Dose for immunocompromised patients or the minimum interval of 8 weeks required for a Booster Dose." for Valid Shot $administeredShot
		Record that this dose rule was Processed for the TargetDose $administeredShot
        Log that this dose rule fired for the dose $administeredShot in the Series $targetSeries
end


rule "COVID-19: If an extra dose is administered to an already completed __Janssen Series__ >= 28 days and < 8 weeks after prior dose, include supplemental text that follows guidelines regarding minimum ages/intervals"
	dialect "mvel"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the series is complete
			- the name of the Series is "COVID19Janssen1DoseSeries"
			- make note of the dose number after which the Series was marked complete as $numberOfDosesRequired
			- the effective number of doses administered in the Series is == $numberOfDosesRequired+1
		There is an administered shot $administeredShot
			- the shot belongs to the series $targetSeries
			- that has already been evaluated and whose Shot Validity Status is VALID
			- the dose number in the Series is == $numberOfDosesRequired+1
			- make note of the date this shot was administered as $currentShotDate
			- make note of the associated series as $associatedTargetSeries
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- that has already been evaluated
			- the administration date of the shot is > $currentShotDate
		There is an administered shot $priorShot
			- the shot belongs to the series $targetSeries
			- the administration date of the shot is < $currentShotDate
			- make note of the date this shot was administered as $priorShotDate
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- the administration date of the shot is > $priorShotDate
			- the administration date of the shot is < $currentShotDate
		Confirm elapsed time between $priorShotDate and $currentShotDate >= "28d"
		Confirm elapsed time between $priorShotDate and $currentShotDate < "8w"
	then
		Include Supplemental Text "The timing of the administration of this shot does not follow the guidelines regarding the minimum interval of 8 weeks required for the 1st Booster Dose." for Valid Shot $administeredShot
		Record that this dose rule was Processed for the TargetDose $administeredShot
        Log that this dose rule fired for the dose $administeredShot in the Series $targetSeries
end

//////////////////////////////////////////////////////////////////////
// END Janssen Series Additional/Booster Logic
//////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////////////
// END Rules for an Additional Dose in Pfizer, Moderna and Janssen Series
//////////////////////////////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////////////////////////
// START ACCEPTED vaccines in a wrong FDA-approved series
// FDA-approved or WHO-approved vaccines in the wrong FDA-approved series that is not complete always get evaluated as ACCEPTED/VACCINE_NOT_COUNTED_BASED_ON_MOST_RECENT_VACCINE_GIVEN rather than Invalid. 
//////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19: If an FDA-approved, NOS or WHO-approved vaccine, the vaccine is not permitted be default for the series, and the series is not complete, evaluate it as ACCEPTED/VACCINE_NOT_COUNTED_BASED_ON_MOST_RECENT_VACCINE_GIVEN"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the vaccine administered a member of ("ICE207", "ICE208", "ICE210", "ICE211", "ICE213", "ICE217", "ICE218", "ICE219", "ICE221", "ICE227", "ICE228", "ICE502", "ICE510", "ICE511", "ICE512")
			- the series that the shot belongs to is not complete
			- make note of the administered vaccine as $vaccineAdministered
			- make note of the dose number as $doseNumber
			- make note of the associated series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the vaccine $vaccineAdministered is not permitted for dose number $doseNumber in this series
	then
		Include the reason for shot $currentShot Accepted due to "Vaccine Not Counted Based on Most Recent Vaccine Given"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

//////////////////////////////////////////////////////////////////////////////////////////////////
// END ACCEPTED vaccines in a wrong FDA-approved series
//////////////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////////////
// START "Skip dose" logic
//////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19: If 2 or more doses were administered >= 5 years of age in the Pfizer or Mixed Product series, then the 3rd dose is not required and the series is complete"
	dialect "mvel"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the name of the Series a member of ("COVID19PfizerSeries", "COVID19MixedProductSeries")
			- the series is not complete
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtAtAge5y when the Patient is "5y" of age
		There is an Administered Shot $firstDose
			- the Shot belongs to the Series $targetSeries 
			- that has already been evaluated and whose shot validity is VALID
			- the dose number in the Series is == 1
			- the administration date of the shot is >= $dtAtAge5y
		There is an Administered Shot $secondDose
			- the Shot belongs to the Series $targetSeries 
			- that has already been evaluated and whose shot validity is VALID
			- the dose number in the Series is == 2
			- the administration date of the shot is >= $dtAtAge5y
		There does not exist an administered shot
			- the Shot belongs to the Series $targetSeries
			- that has already been evaluated
			- the dose number in the Series is > 2
	then
		Mark the Series $targetSeries complete
		Refresh all facts in the Series $targetSeries
		Record that this dose rule was Processed for the TargetDose $secondDose
		Log that this dose rule fired for the dose $secondDose in the Series $targetSeries
end


/*************************************************************************************************************************************************************************************
 If the patient receives a shot below the absolute minimum age, or below the absolute minimum interval prior to 10/25/2021, override vaccine minimum age and series intervals and 
 evaluate the shot as Valid
/************************************************************************************************************************************************************************************/

rule "COVID-19: If CVX 212 is administered below the age of 18y-4d in the Janssen Series, override absolute vaccine minimum age check _and_ return supplemental text that the shot does not follow guidelines for minimum age"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "minimumAgeVaccineCheck"
	salience 5
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850" and the Series with name "COVID19Janssen1DoseSeries"
			- the Vaccine Administered is "ICE212"
			- make note of the date this shot was administered as $administrationDate
			- make note of the associated series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm Elapsed time between $dtBirthDate and $administrationDate < "18y-4d"
	then
		Include Supplemental Text "The timing of the administration of this shot does not follow the guidelines regarding the minimum age." for Valid Shot $currentShot
		Record that this dose rule was Processed for the TargetDose $currentShot
        Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end


rule "COVID-19(Abstract): For any vaccine administered in a COVID-19 series, override the absolute vaccine minimum age check"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "minimumAgeVaccineCheck"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- make note of the date this shot was administered as $administrationDate
			- make note of the associated series as $associatedTargetSeries
	then
		Record that this dose rule was Processed for the TargetDose $currentShot
        Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end


rule "COVID-19: If a shot administered below the age of 6 months for CVX 207/208/213/217/218/219/221/227/228, override absolute vaccine minimum age check _and_ return supplemental text that the shot does not follow guidelines for minimum age"
		extends "COVID-19(Abstract): For any vaccine administered in a COVID-19 series, override the absolute vaccine minimum age check"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm Elapsed time between $dtBirthDate and $administrationDate < "6m"
	then
		Include Supplemental Text "The timing of the administration of this shot does not follow the guidelines regarding the minimum age." for Valid Shot $currentShot
		Record that this dose rule was Processed for the TargetDose $currentShot
        Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end


// If the patient receives a shot below the absolute minimum age or below the absolute minimum interval, evalute the shot as Valid, with reason code SUPPLEMENTAL_TEXT: 
// Descriptive Text: "The timing of the administration of this shot does not follow the guidelines of the regarding the minimum age and/or minimum interval."
rule "COVID-19: For patients >= 18y, if shot in the Pfizer, Mixed Product or Moderna series is administered prior to 10/25/2021 and the series is not complete, override series interval logic"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseIntervalCheck"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the administered shot number is > 1
			- make note of the administered shot number as $administeredShotNumber
			- make note of the date this shot was administered as $administrationDate
			- make note of the associated series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the name of the Series a member of ("COVID19PfizerSeries", "COVID19MixedProductSeries", "COVID19ModernaSeries")
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge18y when the patient is "18y" of age
		There is an Administered Shot $priorDose
			- the shot belongs to the series $associatedTargetSeries
			- that has already been evaluated
			- the administered shot number is == $administeredShotNumber-1
			- the administration date of the shot is >= $dtDateAtAge18y
			- make note of the date this shot was administered as $administrationDatePrior
		There does not exist an Administered Shot
			- the shot belongs to the series $associatedTargetSeries
			- that has already been evaluated
			- the administered shot number is == $administeredShotNumber-1
			- the administration date of the shot is > $administrationDatePrior
		Confirm the date $administrationDate is before "25-Oct-2021"
	then
		Record that this dose rule was Processed for the TargetDose $currentShot
        Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end


//////////////////////////////////////////////////////////////////////////////////////////////////
// Moderna: absolute minimum interval from a prior Moderna shot to the current shot is 24 days
//////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19: For patients >= 18y, enforce absolute minimum interval of 24 days from any prior Moderna shots (in any Series) if after 10/25/2021"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the series that the shot belongs to is not complete
			- make note of the date this shot was administered as $dtAdministrationDateCurrentShot
			- make note of the Associated Series as $associatedTargetSeries
		There is an administered shot $priorModernaShot
			- the shot belongs to the Series $associatedTargetSeries
			- that has already been evaluated
			- the vaccine administered a member of ("ICE207", "ICE221", "ICE227", "ICE228")
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge18y when the patient is "18y" of age
		There is an Administered Shot $priorShot
			- that is the same shot as $priorModernaShot
			- the administration date of the shot is >= $dtDateAtAge18y
			- make note of the date this shot was administered as $dtAdministrationDatePriorCVXdose
		Confirm the date $dtAdministrationDateCurrentShot is on the same date or after "25-Oct-2021"
		Confirm Elapsed time between $dtAdministrationDatePriorCVXdose and $dtAdministrationDateCurrentShot < "24d"
	then
		Include the reason for shot $currentShot Invalid due to "Below Minimum Interval"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

rule "COVID-19: For patients < 18y, always enforce absolute minimum interval of 24 days from any prior Moderna shots (in any Series)"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the series that the shot belongs to is not complete
			- make note of the date this shot was administered as $dtAdministrationDateCurrentShot
			- make note of the Associated Series as $associatedTargetSeries
		There is an administered shot $priorModernaShot
			- the shot belongs to the Series $associatedTargetSeries
			- that has already been evaluated
			- the vaccine administered a member of ("ICE207", "ICE221", "ICE227", "ICE228")
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge18y when the patient is "18y" of age
		There is an Administered Shot $priorShot
			- that is the same shot as $priorModernaShot
			- the administration date of the shot is < $dtDateAtAge18y
			- make note of the date this shot was administered as $dtAdministrationDatePriorCVXdose
		Confirm Elapsed time between $dtAdministrationDatePriorCVXdose and $dtAdministrationDateCurrentShot < "24d"
	then
		Include the reason for shot $currentShot Invalid due to "Below Minimum Interval"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

/*************************************************************************************************************************************************************************************
END If the patient receives a shot below the absolute minimum age or below the absolute minimum interval 
/************************************************************************************************************************************************************************************/

