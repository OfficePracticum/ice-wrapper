/**
 * Copyright (C) 2022 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */

package knowledgeModule.gov.nyc.cir.ice

import java.util.List
import java.util.Set
import org.drools.core.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../../knowledgeCommon/org.cdsframework.ice/org.cdsframework^ICE^1.0.0.dsl

//declare any global variables here
global java.util.Date evalTime
global org.joda.time.MonthDay rsvSeasonStartMonthDay
global org.joda.time.MonthDay rsvSeasonEndMonthDay


rule "RSV: Evaluate the RSV Injection as Outside RSV Season if it does not fall within the Season Start and Stop Dates"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseInSeasonCheck"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- make note of the date this shot was administered as $currentShotDate
			- make note of the Associated Series as $associatedTargetSeries
    The Given date $currentShotDate does not fall within the ranges of rsvSeasonStartMonthDay and rsvSeasonEndMonthDay
	then
		Include the reason for shot $currentShot Invalid due to "Outside RSV Season"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

rule "RSV: (Beyfortus) If the vaccine was administered for a patient that is >= 20 months old accept vaccine"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	activation-group "validDoseCheck"
  salience 30
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dateAt20mOfAge when the patient is "20m" of age
		There is an Administered Shot $currentShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- the vaccine administered a member of ("ICE306", "ICE307")
			- that has already been evaluated and whose shot validity is VALID
			- the administration date of the shot is >= $dateAt20mOfAge
			- make note of the associated series as $associatedTargetSeries
	then
		Set the shot status of $currentShot to Accepted
		Include the reason for shot $currentShot Accepted due to "Above Recommended Age for Series"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

rule "RSV: Invalidate same season vaccine if the Beyfortus vaccine was administered and valid on the same RSV season"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	activation-group "validDoseCheck"
  salience 29
	when
		There is an Administered Shot $currentShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- that has already been evaluated and whose shot validity is VALID or ACCEPTED
			- make note of the date this shot was administered as $currentShotAdministrationDate
			- make note of the associated series as $associatedTargetSeries
		There is an Administered Shot $previousBeyfortusShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- the vaccine administered a member of ("ICE306", "ICE307")
			- the shot is not ignored
			- the administration date of the shot is < $currentShotAdministrationDate
			- make note of the date this shot was administered as $previousBeyfortusShotDate
		Confirm Elapsed time between $previousBeyfortusShotDate and $currentShotAdministrationDate < "6m"
	then
		Set the shot status of $currentShot to Invalid
		Include the reason for shot $currentShot Invalid due to "Below Minimum Interval"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

rule "RSV: (Beyfortus) If the vaccine was administered for a patient that is >= 8 months old accept vaccine as extra dose"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	activation-group "validDoseCheck"
  salience 15
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dateAt8mOfAge when the patient is "8m" of age
		There is an Administered Shot $currentShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- the vaccine administered a member of ("ICE307")
			- that has already been evaluated and whose shot validity is VALID
			- the administration date of the shot is >= $dateAt8mOfAge
			- make note of the associated series as $associatedTargetSeries
	then
		Set the shot status of $currentShot to Accepted
		Include the reason for shot $currentShot Accepted due to "Extra Dose"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

rule "RSV (Synagis): If the vaccine administered is Synagis, move to accepted extra dose, always"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	activation-group "validDoseCheck"
  salience 50
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dateAt6mOfAge when the patient is "6m" of age
			- make note of the Patient's birthdate as $birthdate
		There is an Administered Shot $currentShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- the vaccine administered a member of ("ICE93")
			- that has already been evaluated and whose shot validity is VALID
			- make note of the associated series as $associatedTargetSeries
	then
		Set the shot status of $currentShot to Accepted
		Include the reason for shot $currentShot Accepted due to "Extra Dose"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

rule "RSV: If the vaccine was administered on second season, baby born on-season accept vaccine as extra dose"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	activation-group "validDoseCheck"
  salience 15
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dateAt6mOfAge when the patient is "6m" of age
			- make note of the Patient's birthdate as $birthdate
		There is an Administered Shot $currentShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- that has already been evaluated and whose shot validity is VALID
			- the administration date of the shot is >= $dateAt6mOfAge
			- make note of the associated series as $associatedTargetSeries
		The Given date $birthdate falls within the ranges of rsvSeasonStartMonthDay and rsvSeasonEndMonthDay
	then
		Set the shot status of $currentShot to Accepted
		Include the reason for shot $currentShot Accepted due to "Extra Dose"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

rule "RSV: If the vaccine was administered on second season, baby born off-season accept vaccine as extra dose"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
  salience 15
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dateAt1yOfAge when the patient is "1y0d" of age
			- make note of the Patient's birthdate as $birthdate
		There is an Administered Shot $currentShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- that has already been evaluated and whose shot validity is VALID
			- the administration date of the shot is >= $dateAt1yOfAge
			- make note of the associated series as $associatedTargetSeries
		The Given date $birthdate does not fall within the ranges of rsvSeasonStartMonthDay and rsvSeasonEndMonthDay
	then
		Set the shot status of $currentShot to Accepted
    Include the reason for shot $currentShot Accepted due to "Extra Dose"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end
